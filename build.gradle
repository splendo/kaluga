buildscript {

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        // mostly migrated to new style plugin declarations, but some cross plugin interaction still requires this
        classpath "com.android.tools.build:gradle:${project.'kaluga.androidGradlePluginVersion'}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${project.'kaluga.kotlinVersion'}"
    }
}

plugins {
    id "convention.publication"
    id 'org.jlleitschuh.gradle.ktlint'
    id 'org.jlleitschuh.gradle.ktlint-idea'
    id 'org.jetbrains.kotlin.multiplatform' apply false
    id 'org.jetbrains.kotlinx.binary-compatibility-validator'
}

// TODO: To be removed once we will migrate to kotlin version 1.6.20
// https://youtrack.jetbrains.com/issue/KT-49109#focus=Comments-27-5667134.0-0
rootProject.plugins.withType(Class.forName("org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootPlugin")) {
    rootProject.extensions.getByType(org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootExtension.class).nodeVersion = "16.13.2"
}

allprojects {
    repositories {
        mavenCentral()
        google()
        // only enable temporarily if needed:
        / * mavenLocal() */
    }

    tasks.withType(Test) {
        testLogging {
            events "standardOut", "started", "passed", "skipped", "failed"
        }
        testLogging.exceptionFormat = 'full'
    }

    tasks.withType(org.jlleitschuh.gradle.ktlint.tasks.BaseKtLintCheckTask).configureEach {
        it.workerMaxHeapSize.set("512m")
    }
}

apiValidation {

    subprojects.forEach {
        def name = it.name

        ignoredClasses += ["com.splendo.kaluga.${name}.BuildConfig".toString()]
        ignoredClasses += ["com.splendo.kaluga.${name.replaceAll('-', ".")}.BuildConfig".toString()]
        ignoredClasses += ["com.splendo.kaluga.permissions.${name.replaceAll("-permissions", "")}.BuildConfig".toString()]
    }

    ignoredClasses += ['com.splendo.kaluga.permissions.BuildConfig']
    ignoredClasses += ['com.splendo.kaluga.test.BuildConfig']
    ignoredClasses += ['com.splendo.kaluga.datetime.timer.BuildConfig']

    ignoredProjects += ["androidtesthelper"]
}

apply from:"gradle/ext.gradle"
apply from:"gradle/newModule.gradle.kts"
apply from:"gradle/copyReports.gradle.kts"

group 'com.splendo.kaluga'
version gradle.ext.library_version
